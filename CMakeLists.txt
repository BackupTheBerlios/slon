CMAKE_MINIMUM_REQUIRED (VERSION 2.6)
PROJECT (SlonEngine)
SET (CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/CMakeModules;${CMAKE_CURRENT_SOURCE_DIR}/CMakeModules/FindModules;${CMAKE_MODULE_PATH}")

IF (WIN32)
    # setup 3rd party directory as search path
    SET ( SLON_ENGINE_3RD_PARTY_FOUND FALSE )
    IF ( MSVC AND (MSVC_VERSION EQUAL 1600) )
        SET ( CMAKE_PREFIX_PATH "${CMAKE_CURRENT_SOURCE_DIR}/3rdParty/msvs_2010;${CMAKE_PREFIX_PATH}" )
        SET ( 3RD_PARTY_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/3rdParty/msvs_2010 )
        SET ( SLON_ENGINE_3RD_PARTY_FOUND TRUE )
    ELSEIF ( MSVC AND (MSVC_VERSION EQUAL 1500) )
        SET ( CMAKE_PREFIX_PATH "${CMAKE_CURRENT_SOURCE_DIR}/3rdParty/msvs_2008;${CMAKE_PREFIX_PATH}" )
        SET ( 3RD_PARTY_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/3rdParty/msvs_2008 )
        SET ( SLON_ENGINE_3RD_PARTY_FOUND TRUE )
    ENDIF ( MSVC AND (MSVC_VERSION EQUAL 1600) )
    
    IF (SLON_ENGINE_3RD_PARTY_FOUND)
        SET ( BOOST_ROOT        ${3RD_PARTY_DIRECTORY}/boost )
        SET ( BOOST_INCLUDEDIR  ${3RD_PARTY_DIRECTORY}/boost/boost )
        SET ( BOOST_LIBRARYDIR  ${3RD_PARTY_DIRECTORY}/boost/lib )
        
        LIST ( APPEND CMAKE_INCLUDE_PATH 
            ${3RD_PARTY_DIRECTORY}/boost/ 
            ${3RD_PARTY_DIRECTORY}/BULLET_PHYSICS/include
            ${3RD_PARTY_DIRECTORY}/SDL/include
        )

        LIST ( APPEND CMAKE_LIBRARY_PATH 
            ${3RD_PARTY_DIRECTORY}/boost/lib
            ${3RD_PARTY_DIRECTORY}/BULLET_PHYSICS/lib
            ${3RD_PARTY_DIRECTORY}/SDL/lib
        )

        LIST ( APPEND CMAKE_PROGRAM_PATH
            ${CMAKE_CURRENT_SOURCE_DIR}/3rdParty/gnuplot
            ${CMAKE_CURRENT_SOURCE_DIR}/3rdParty/Python
            ${CMAKE_CURRENT_SOURCE_DIR}/3rdParty/GCCXML/bin
        )

        # copy dlls into target binary dir
        ADD_CUSTOM_TARGET (SlonEngineCopyDLL)
        SET (3RD_PARTY_TARGET SlonEngineCopyDLL)
        SET_TARGET_PROPERTIES (${3RD_PARTY_TARGET} PROPERTIES
            FOLDER Utility
        )
        
        MACRO (COPY_DLLS SOURCE DESTINATION COPY_FILES)
            FOREACH(FILENAME ${${COPY_FILES}})
                ADD_CUSTOM_COMMAND( TARGET ${3RD_PARTY_TARGET}
                                    COMMAND ${CMAKE_COMMAND} -E copy "${SOURCE}/${FILENAME}" "${DESTINATION}/${FILENAME}")
            ENDFOREACH(FILENAME)
        ENDMACRO (COPY_DLLS)

        SET (SDL_DLLS
            SDL.dll
            SDL_image.dll
        )

        IF (NOT SLON_ENGINE_CONFIGURE_INTRUSIVE)
            SET (RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin)
            SET (3RD_PARTY_TARGET_DIRECTORY ${RUNTIME_OUTPUT_DIRECTORY}/${CMAKE_CFG_INTDIR})
        ENDIF (NOT SLON_ENGINE_CONFIGURE_INTRUSIVE)
        COPY_DLLS(${3RD_PARTY_DIRECTORY}/SDL/bin ${3RD_PARTY_TARGET_DIRECTORY} SDL_DLLS)
    ENDIF (SLON_ENGINE_3RD_PARTY_FOUND)
ENDIF (WIN32)

IF (NOT SLON_ENGINE_PROJECT_GROUP)
    SET (SLON_ENGINE_PROJECT_GROUP Lib)
ENDIf (NOT SLON_ENGINE_PROJECT_GROUP)

SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS TRUE)

SET (SIMPLE_GL_CONFIGURE_INTRUSIVE 	ON)	
SET (SIMPLE_GL_PATH					${CMAKE_CURRENT_SOURCE_DIR}/SimpleGL/SimpleGL)
SET (SIMPLE_GL_PROJECT_GROUP        ${SLON_ENGINE_PROJECT_GROUP})
SET (XMLPP_CONFIGURE_INTRUSIVE 		ON)
SET (XMLPP_PATH						${CMAKE_CURRENT_SOURCE_DIR}/XML++)
SET (XMLPP_PROJECT_GROUP            ${SLON_ENGINE_PROJECT_GROUP})

ADD_SUBDIRECTORY(SimpleGL)
ADD_SUBDIRECTORY(XML++)
ADD_SUBDIRECTORY(SlonEngine)
      